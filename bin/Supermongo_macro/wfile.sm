wfile 01
    # 
    # write text file with auto-formats
    # 
    # 2017-09-07 now allow set wfile_fmt_somevector = '%20s' to specify the format for one vector named 'somevector'
    # 
    if(is_vector(wfile_headers) && !is_vector(wfile_vectors)) {
        set wfile_vectors = wfile_headers
    }
    # 
    # check input arguments
    if(!$?1 || !is_vector(wfile_vectors)) {
        print 'usage: \n' {}
        print '       set wfile_vectors = {id ra dec name a b c}\n' {}
        print '       set wfile_fmt_id = \'%16.0f\'\n' {}
        print '       set wfile_fmt_ra = \'%16.8f\'\n' {}
        print '       set wfile_fmt_dec = \'%16.8f\'\n' {}
        print '       wfile output.txt\n' {}
        return
    }
    # 
    # set wfile_headers and write column headers
    set wfile_headers = wfile_vectors
    define print_noheader 1
    define wfile_has_header 0
    declare wfile_fmws 0.d
    do wfile_j=0,dimen(wfile_headers)-1 {
        define wfile_vec $(wfile_headers[$wfile_j])
        set wfile_str = string('$wfile_vec')
        set wfile_fmw = 15
        # set column width larger than strlen(wfile_str)+2
        if(wfile_fmw<strlen(wfile_str)+2) {
            set wfile_fmw = strlen(wfile_str)+2
        }
        # allow user-specified format
        if(is_vector(wfile_fmt_$wfile_vec)) {
            set wfile_fmt = string(wfile_fmt_$wfile_vec)
            if(index(wfile_fmt,'%')==0) {set wfile_fmt = substr(wfile_fmt,index(wfile_fmt,'%')+1,0)}
            if(index(wfile_fmt,'s')==0) {set wfile_fmt = substr(wfile_fmt,0,index(wfile_fmt,'s'))}
            if(index(wfile_fmt,'f')==0) {set wfile_fmt = substr(wfile_fmt,0,index(wfile_fmt,'f'))}
            if(index(wfile_fmt,'g')==0) {set wfile_fmt = substr(wfile_fmt,0,index(wfile_fmt,'g'))}
            if(index(wfile_fmt,'e')==0) {set wfile_fmt = substr(wfile_fmt,0,index(wfile_fmt,'e'))}
            if(int(wfile_fmt)>0) {set wfile_fmw = int(wfile_fmt)}
        }
        if($wfile_has_header==0) {
            print $1 '\# %$(wfile_fmw-1)s' {wfile_str}
        } else {
            print +$1 ' %$(wfile_fmw)s' {wfile_str}
        }
        if(is_vector(wfile_separator)) {
            print +$1 ' %s' {wfile_separator}
        }
        set wfile_fmws = wfile_fmws concat wfile_fmw
        define wfile_has_header 1
    }
    print +$1 '\n\# \n' {}
    !echo -n "Writing to "$1" 0%"
    #return
    # 
    # write data content
    set wfile_vec_1 = $(wfile_headers[0])
    do wfile_i=0,dimen(wfile_vec_1)-1 {
        do wfile_j=0,dimen(wfile_headers)-1 {
            define wfile_vec $(wfile_headers[$wfile_j])
            set wfile_str = string('$wfile_vec')
            set wfile_val = $wfile_vec[$wfile_i]
            set wfile_fmw = wfile_fmws[$wfile_j]
            set wfile_fmt = '%$(wfile_fmw)g'
            if(wfile_str=='id'  || index(wfile_str,'id_')==0)  { set wfile_fmt = '%$(wfile_fmw).0f' }
            if(wfile_str=='ra'  || index(wfile_str,'ra_')==0)  { set wfile_fmt = '%$(wfile_fmw).7f' }
            if(wfile_str=='RA'  || index(wfile_str,'RA_')==0)  { set wfile_fmt = '%$(wfile_fmw).7f' }
            if(wfile_str=='de'  || index(wfile_str,'de_')==0)  { set wfile_fmt = '%$(wfile_fmw).7f' }
            if(wfile_str=='dec' || index(wfile_str,'dec_')==0) { set wfile_fmt = '%$(wfile_fmw).7f' }
            if(wfile_str=='Dec' || index(wfile_str,'Dec_')==0) { set wfile_fmt = '%$(wfile_fmw).7f' }
            if(wfile_str=='DEC' || index(wfile_str,'DEC_')==0) { set wfile_fmt = '%$(wfile_fmw).7f' }
            if(wfile_str=='_id' || index(wfile_str,'_id_')==0) { set wfile_fmt = '%$(wfile_fmw).0f' }
            if(wfile_str=='_ra' || index(wfile_str,'_ra_')==0) { set wfile_fmt = '%$(wfile_fmw).7f' }
            if(wfile_str=='_de' || index(wfile_str,'_de_')==0) { set wfile_fmt = '%$(wfile_fmw).7f' }
            if(index(wfile_str,'_str')>0 && index(wfile_str,'_str')==strlen(wfile_str)-strlen('_str')) { set wfile_fmt = '%$(wfile_fmw)s' }
            if(index(wfile_str,'_STR')>0 && index(wfile_str,'_STR')==strlen(wfile_str)-strlen('_STR')) { set wfile_fmt = '%$(wfile_fmw)s' }
            if(index(wfile_str,'Name')>=0) { set wfile_fmt = '%$(wfile_fmw)s' }
            # allow user-specified format
            if(is_vector(wfile_fmt_$wfile_vec)) {
                set wfile_fmt = string(wfile_fmt_$wfile_vec)
                if(index(wfile_fmt,'%')!=0) {set wfile_fmt = '%'+wfile_fmt}
                if(index(wfile_fmt,'s')>=0) {set wfile_fmt = wfile_fmt}
                if(index(wfile_fmt,'f')>=0) {set wfile_fmt = wfile_fmt}
                if(index(wfile_fmt,'g')>=0) {set wfile_fmt = wfile_fmt}
                if(index(wfile_fmt,'e')>=0) {set wfile_fmt = wfile_fmt}
            }
            # convert to float 
            if(index(wfile_fmt,'s')<0 && index(wfile_fmt,'d')<0 && index(wfile_fmt,'i')<0) { set wfile_val = float(wfile_val) }
            # print each column of each row
            print +$1 ' $(wfile_fmt)' {wfile_val}
            if(is_vector(wfile_separator) && $wfile_j!=dimen(wfile_headers)-1) {
                print +$1 ' %s' {wfile_separator}
            }
        }
        print +$1 '\n' {}
        # print progress
        set wfile_print_interval = dimen(wfile_vec_1)/10
        set wfile_print_interval = (wfile_print_interval>1) ? wfile_print_interval : 1
        if($wfile_i%(wfile_print_interval)==0) {
            !echo -n '" "'$(sprintf('%0.1f',($wfile_i+1)/dimen(wfile_vec_1)*100.0))"%"
        }
    }
    !echo '" 100%"'
    define print_noheader 0
    echo "Output to "$1"!"



